font	equ	$fc00

	org	$100
main
	sei
	mvy	#0	^4e
	lda:rne	^4b
	sta	^40
	ldx	#29
	sta:rpl	^00,x-

; copy font from ROM, reordering from ANTIC codes to ASCII, semigraphics not needed
copy_font_1
	stx	^31	; #$ff
	lda:pha	$e000,y	; punctuation
	lda:pha	$e100,y	; uppercase
	lda	$e300,y	; lowercase
	dex:stx	^31
	inx
	sta	font+$300,y	; lowercase
	pla:sta	font+$200,y	; uppercase
	pla:sta	font+$100,y	; punctuation
	iny
	bne	copy_font_1
	jsr	$fcd3
; A==0 here!

	sta	^2e
	ldx	#8
init_pokey_1
	sta	$d210,x
	sta	$d200,x
	dex:bpl	init_pokey_1
	lda	#3
	sta	$d21f
	sta	$d200

; TODO: support 400/800/600XL
	mva	#$fe	^31
	lda	player+1
	and	player+2
	cmp	#$ff
	beq	no_player
	mwa	#nmi	$fffa
	mva	#$40	^4e
no_player
	lda	song
	cli
	jmp	init

nmi
	cli
	pha
	txa:pha
	tya:pha
	jsr	player
	pla:tay
	pla:tax
	pla:rti

init	jmp	0
player	jmp	0
song	dta	0

	run	main
	end
