	org	$bb00
	dta	c'ASAP 3.0.0 TYPE B',0
main
	sei
	mva	#0	^4e
	lda:rne	^4b
	sta	^40
	ldx	#29
	sta:rpl	^00,x-
	ldx	#8
init_pokey_1
	sta	$d210,x
	sta	$d200,x
	dex:bpl	init_pokey_1
	lda	#3
	sta	$d21f
	sta	$d200

; check PAL vs NTSC ANTIC
; possible NTSC GTIA with PAL ANTIC
	lda	#130
	cmp:rne	^4b
:3	sta	^4a
	lda	^4b
	sne:inc	ntsc

	lda	song
	jsr	init

; TODO: support 400/800/600XL
	mva	#$fe	^31
	mwa	#nmi	$fffa
	mva	#$40	^4e

	lda	#125

player_loop
	cmp:rne	^4b
	jsr	player
	ldx	ntsc
	lda	fastplay_low_bit
	lsr	@
	bcc	fastplay_is_even
	lda:inc	calls
	lsr	@
fastplay_is_even
	lda	vcount_period,x
	adc	expected_vcount
	bcs	next_frame
	cmp	vcount_wrap,x
	bcc	same_frame
next_frame
	sbc	vcount_wrap,x
	sec
same_frame
	sta	expected_vcount
	lda	frame_period,x
	adc:sta	expected_frame
	cmp:rne	frame
	lda	expected_vcount
	bcs	player_loop	!

nmi
	inc	frame
	rti

ntsc	dta	0
frame	dta	0
calls	dta	0
expected_vcount	dta	125
expected_frame	dta	0
vcount_wrap	dta	156,131
init	jmp	0
player	jmp	0
song	dta	0
fastplay_low_bit	dta	0
vcount_period	dta	0,0
frame_period	dta	0,0

	run	main
	end
